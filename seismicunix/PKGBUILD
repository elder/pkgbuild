# Maintainer:  Elder Marco <eldermarco at gmail dot com>

pkgname=seismicunix
_pkgname=cwp_su_all
pkgver=43
pkgrel=1
pkgdesc="Open source software for seismic research and seismic processing"

license=('BSD')
url="http://www.cwp.mines.edu/cwpcodes/"
source=("ftp://ftp.cwp.mines.edu/pub/cwpcodes/${_pkgname}_${pkgver}.tgz"
        "$pkgname-$pkgver-makefile-config.patch"
        "$pkgname-$pkgver-no-questions.patch"
        "$pkgname.sh"
        "$pkgname.csh")

makedepends=('libxmu' 'libxi' 'mesa' 'freeglut' 'lesstif' 'gcc-fortran')
depends=('libxmu' 'libxi' 'mesa' 'freeglut' 'lesstif')

arch=('i686' 'x86_64')
md5sums=('aacca309939947b8542c811dbe8f20c2'
         'fa6ee88321df6834a26c4b120f899a55'
         'e3729445e0eedb177f230714271baf1d'
         '9b1f8dd11bc33f4b52c5db9870817d54'
         '2984eab8f0481f9d9c510d1ce6c48519')

build () {
    cd "$srcdir/src"
    patch -Np1 -i ../$pkgname-$pkgver-makefile-config.patch
    patch -Np1 -i ../$pkgname-$pkgver-no-questions.patch

    # Remove suphidecon from Makefile. The is a temporary "solution".
    # Developers will fix this soon.
    sed -i '/\/suphidecon/ d' su/main/decon_shaping/Makefile

    export CWPROOT="$srcdir"

    # Basict set of codes
    make install

    # X-toolkit applications
    make xtinstall

    # Motif application
    make xminstall

    # Fortran codes
    make finstall

    # Mesa/OpenGL items
    make mglinstall

    # libcwputils
    make utils
}

package () {
    cd "$srcdir"

    # Binary files
    mkdir -p "$pkgdir/opt/$pkgname/bin"
    cp -pr bin/* "$pkgdir/opt/$pkgname/bin/"

    # Include Headers
    mkdir -p "$pkgdir/opt/$pkgname/include"
    cp -pr include/* "$pkgdir/opt/$pkgname/include/"

    # Libraries
    mkdir -p "$pkgdir/opt/$pkgname/lib/"
    cp -pr lib/* "$pkgdir/opt/$pkgname/lib/"

    # Source Code and others
    mkdir -p "$pkgdir/opt/$pkgname/src"
    for _dir in doc cwp par psplot xplot Xtcwp 3D \
                Xmcwp su tri tetra faq comp xtri; do
        cp -pr "src/$_dir" "$pkgdir/opt/$pkgname/src"
    done

    # Environment Variables
    install -d -m0755 "$pkgdir/etc/profile.d"
    install -m0755 "${pkgname}.sh" "$pkgdir/etc/profile.d/"
    install -m0755 "${pkgname}.csh" "$pkgdir/etc/profile.d/"

    # License file and acknowledgments
    install -d -m0755 "$pkgdir/usr/share/licenses/$pkgname"
    install -p -m0644 src/LEGAL_STATEMENT  \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING"
    install -p -m0644 src/ACKNOWLEDGEMENTS \
        "$pkgdir/usr/share/licenses/$pkgname/"
}
# expandtab:tabstop=4:shiftwidth=4
