# Maintainer:  Elder Marco <eldermarco at gmail dot com>

pkgname=gpaste
pkgver=2.0
pkgrel=1
pkgdesc="Clipboard management system with a gnome-shell extension"

license=('GPL3')
url="https://github.com/Keruspe/GPaste"
source=("https://github.com/downloads/Keruspe/GPaste/$pkgname-$pkgver.tar.xz")

makedepends=('dbus-core' 'libxml2' 'gtk3' 'intltool>=0.40.0')
depends=('dbus-core' 'libxml2' 'gtk3' 'gnome-shell>=3.2.0')
install=$pkgname.install

arch=('i686' 'x86_64')
md5sums=('e70a14145bf94665ad2707aa49a5f1fd')

build () {
    cd "$srcdir/$pkgname-$pkgver"

    ./configure --prefix=/usr           \
                --sysconfdir=/etc       \
                --libexecdir=/usr/lib   \
                --disable-applet        \
                --disable-schemas-compile
    make
}

package () {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    # bash/zsh completion support
    install -Dp -m0644 data/completions/$pkgname     \
        "$pkgdir/etc/bash_completion.d/$pkgname"
    install -Dp -m0644 data/completions/_$pkgname    \
        "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
# expandtab:tabstop=4:shiftwidth=4
