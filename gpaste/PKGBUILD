# Maintainer:  Elder Marco <eldermarco@gmail.com>
# Contributor: Elder Marco <eldermarco@gmail.com>

pkgname=gpaste
pkgver=1.2
pkgrel=3
arch=('i686' 'x86_64')
url="https://github.com/Keruspe/GPaste"
pkgdesc="Clipboard management system with a gnome-shell extension"
depends=('gtk3' 'gnome-shell')
install=$pkgname.install
license=('GPL3')
makedepends=('gtk3' 'intltool>=0.40.0' 'vala')
source=("https://github.com/downloads/Keruspe/GPaste/$pkgname-$pkgver.tar.xz"
        "$pkgname-$pkgver-remove-applet-refs.patch")
md5sums=('155532ac19f0444f66c544b5ef8f1827'
         '6e9de97078df82c8b3ca7e0bf9817bbc')

build () {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i ../$pkgname-$pkgver-remove-applet-refs.patch

    ./configure --prefix=/usr           \
                --libexecdir=/usr/lib   \
                --disable-applet        \
                --disable-schemas-compile
    make
}

package () {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    # bash/zsh completion support
    install -Dp -m0644 completions/$pkgname     \
        "$pkgdir/etc/bash_completion.d/$pkgname"
    install -Dp -m0644 completions/_$pkgname    \
        "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
# expandtab:tabstop=4:shiftwidth=4
