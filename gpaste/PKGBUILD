# Maintainer:  Elder Marco <eldermarco@gmail.com>
# Contributor: Elder Marco <eldermarco@gmail.com>

pkgname=gpaste
pkgver=1.6
pkgrel=1
pkgdesc="Clipboard management system with a gnome-shell extension"

license=('GPL3')
url="https://github.com/Keruspe/GPaste"
source=("https://github.com/downloads/Keruspe/GPaste/$pkgname-$pkgver.tar.xz")

makedepends=('dbus-core' 'gtk3' 'intltool>=0.40.0')
depends=('dbus-core' 'gtk3' 'gnome-shell')
install=$pkgname.install

arch=('i686' 'x86_64')
md5sums=('0237d46597986d3ae4aee462d5f52312')

build () {
    cd "$srcdir/$pkgname-$pkgver"

    ./configure --prefix=/usr           \
                --sysconfdir=/etc       \
                --libexecdir=/usr/lib   \
                --disable-applet        \
                --disable-schemas-compile
    make
}

package () {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    # bash/zsh completion support
    install -Dp -m0644 data/completions/$pkgname     \
        "$pkgdir/etc/bash_completion.d/$pkgname"
    install -Dp -m0644 data/completions/_$pkgname    \
        "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
# expandtab:tabstop=4:shiftwidth=4
